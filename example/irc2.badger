import "badger:io"

let socket = Socket.connect("irc.esper.net", 6667)

print("Connected")

socket.writeLine("NICK BadgerBot")
socket.writeLine("USER BadgerBot 8 * :Badger Bot")

func sendMessage(target, message) {
  socket.writeLine("PRIVMSG $(target) :$(message)")
}

func join(channel) {
  socket.writeLine("JOIN $(channel)")
}

func part(channel) {
  socket.writeLine("PART $(channel)")
}

func handleCommand(cmd, args, channel) {
  switch cmd {
    case "hello" {
      sendMessage(channel, "Hello World")
    }

    case "goodbye" {
      sendMessage(channel, "Goodbye World")
    }

    case "eval" {
      let value = eval(args.join(" "))

      if value != void {
        sendMessage(channel, value)
      }
    }
  }
}

socket.handleLine((line) -> {
  let split = line.split(" ")

  if split[0] == "PING" {
    let x = split[1]
    socket.writeLine("PONG $(x)")
  }

  if split[1] == "001" {
    join("#directcode")
  }

  if split[1] == "PRIVMSG" {
    let channel = split[2]
    let th = split[3]
    let msg = th.substring(1)
    let left = split.skip(4)
    let cargs = left.toList()
    let iscmd = msg.startsWith("!")

    if iscmd {
      let s = msg.split(" ")
      let f = s[0]
      cmd = f.substring(1)

      handleCommand(cmd, cargs, channel)
    }
  }

  print(line)
})
