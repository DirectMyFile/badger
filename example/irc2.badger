import "badger:io"

let socket = Socket.connect("irc.esper.net", 6667)

print("Connected")

socket.writeLine("NICK BadgerBot")
socket.writeLine("USER BadgerBot 8 * :Badger Bot")

socket.handleLine((line) -> {
  let split = line.split(" ")

  if split[0] == "PING" {
    let x = split[1]
    socket.writeLine("PONG $(x)")
  }

  if split[1] == "001" {
    socket.writeLine("JOIN :#directcode")
  }

  if split[1] == "PRIVMSG" {
    let channel = split[2]
    let th = split[3]
    let msg = th.substring(1)
    let left = split.skip(4)
    let cargs = left.toList()
    let iscmd = msg.startsWith("!")

    if iscmd {
      let s = msg.split(" ")
      let f = s[0]
      let cmd = f.substring(1)

      if cmd == "hello" {
        socket.writeLine("PRIVMSG $(channel) :Hello!")
      }

      if cmd == "google" {
        let query = cargs.join(" ")

        let http = make(HttpClient)
        let response = http.get("http://ajax.googleapis.com/ajax/services/search/web?v=1.0&q=$(query)")

        let json = JSON.parse(response.body)
        let results = json.responseData.results

        if results.length == 0 {
          socket.writeLine("PRIVMSG $(channel) :No Results Found")
        } else {
          let result = results.first
          let url = result.unescapedUrl

          socket.writeLine("PRIVMSG $(channel) :$(result.titleNoFormatting) | $(url)")
        }
      }
    }
  }

  print(line)
})
